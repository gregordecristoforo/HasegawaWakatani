## Run all (alt+enter)
using HasegawaWakatani
using CUDA

domain = Domain(1024, 1024; Lx=50, Ly=50, MemoryType=CuArray, precision=Float32)

# Check documentation to see other initial conditions
ic = initial_condition(isolated_blob, domain)

# Linear operator
function Linear(du, u, operators, p, t)
    @unpack κ, ν = p
    θ, Ω = eachslice(u; dims=3)
    dθ, dΩ = eachslice(du; dims=3)
    @unpack laplacian = operators
    dθ .= κ .* laplacian(θ)
    dΩ .= ν .* laplacian(Ω)
end

# Non-linear operator
function NonLinear(du, u, operators, p, t)
    θ, Ω = eachslice(u; dims=3)
    dθ, dΩ = eachslice(du; dims=3)
    @unpack diff_y, poisson_bracket, solve_phi = operators
    ϕ = solve_phi(Ω)
    dθ .= poisson_bracket(θ, ϕ)
    dΩ .= poisson_bracket(Ω, ϕ) .- diff_y(θ)
end

# Parameters
parameters = (ν=1e-2, κ=1e-2)

# Time interval
tspan = [0.0, 20.0]

# Array of diagnostics want
diagnostics = @diagnostics [
    probe_density(; positions=[(5, 0), (8.5, 0), (11.25, 0), (14.375, 0)], stride=10),
    radial_COM(; stride=1),
    progress(; stride=100),
    cfl(; stride=1),
    plot_density(; stride=1000),
    plot_vorticity(; stride=1000),
    plot_potential(; stride=1000)
]

# Collection of specifications defining the problem to be solved
prob = SpectralODEProblem(Linear, NonLinear, ic, domain, tspan; p=parameters, dt=1e-3,
                          boussinesq=true, aliases=[:∂x => :diff_x],
                          diagnostics=diagnostics)

# The output
output_file_name = joinpath(@__DIR__, "output", "Garcia 2005 PoP.h5")
output = Output(prob; filename=output_file_name, stride=-1, simulation_name=:parameters,
                storage_limit="0.5 GB", store_locally=true)

# Solve and plot
sol = spectral_solve(prob, MSS3(), output; resume=false)